import app.futured.kmptemplate.gradle.configuration.ProjectSettings
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.dokka)

    id(libs.plugins.conventions.lint.get().pluginId)
    id(libs.plugins.conventions.annotationProcessing.get().pluginId)
}

annotations {
    useKoin = true
    useComponentFactory = true
}

dependencies {
    implementation(platform(libs.androidx.compose.bom))
}

kotlin {
    jvmToolchain(ProjectSettings.Kotlin.JvmToolchainVersion)

    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.fromTarget(ProjectSettings.Android.KotlinJvmTargetNum))
        }
    }

    iosX64()
    iosArm64()
    iosSimulatorArm64()

    sourceSets {
        commonMain {
            kotlin.srcDir("build/generated/ksp/metadata/commonMain/kotlin")

            dependencies {
                implementation(libs.decompose)
                implementation(libs.koin.core)
                implementation(libs.koin.annotations)
                implementation(libs.kotlinx.immutableCollections)
                implementation(libs.kotlinx.coroutines.core)
                implementation(libs.kotlinx.dateTime)
                implementation(libs.jetbrains.compose.runtime)

                implementation(projects.shared.network.rest)
                implementation(projects.shared.persistence)
                implementation(projects.shared.arkitektDecompose)
                implementation(projects.shared.arkitektDecompose.annotation)
                implementation(projects.shared.resources)

                implementation(libs.logging.kermit)
                implementation(libs.skie.annotations)
                implementation(libs.network.ktor.http)
                implementation(libs.network.ktorfit.lib)
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlin.test)
            }
        }
    }
}

android {
    namespace = libs.versions.project.shared.feature.namespace.get()
    compileSdk = ProjectSettings.Android.CompileSdkVersion
    defaultConfig {
        minSdk = ProjectSettings.Android.MinSdkVersion
    }
    compileOptions {
        sourceCompatibility = ProjectSettings.Android.JavaCompatibility
        targetCompatibility = ProjectSettings.Android.JavaCompatibility
    }

    buildFeatures {
        compose = true
    }
}

tasks.withType<DokkaTask>().configureEach {
    dokkaSourceSets.configureEach {
        outputDirectory.set(layout.projectDirectory.dir("../../doc/documentation/html"))

        val dokkaBaseConfiguration = """
    {
      "customStyleSheets": ["${file("../../assets/docs-style.css")}"],
      "footerMessage": "(c) 2024 Futured - KMP Template"
    }
    """
        pluginsMapConfiguration.set(
            mapOf(
                // fully qualified plugin name to json configuration
                "org.jetbrains.dokka.base.DokkaBase" to dokkaBaseConfiguration,
            ),
        )
    }
}
